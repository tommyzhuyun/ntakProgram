*****< Circuit for CMIR >*****

*****< Option >*****
.lib '../Lib_Data/Option.lib' ngspice

*****< MOSFET MODEL >*****
.lib '../Lib_Data/tsmc018.lib' model27

*****< Main Stage >*****
.lib '../Sp_Data/OPamp.sp' opamp

*****< Parameters >*****
.param half='psvoltage/2'
.param rload=20k
.param bgain=10

*****< Power_Supply >*****
VDD vdd gnd {half}
VSS gnd vss {half}

******< Input >******
VIN in1 gnd dc 0
E1 gnd in2 in1 gnd 1

******< Circuit Topology >******
* Positive side
X1a inm1 inp1 outx1 vdd vss opamp
R1a in1 inp1 {rload/2}
R2a inp1 gnd {rload/2}
R3a in1 inm1 {rload/2}
R4a inm1 out1 {rload}
E1a out1 gnd outx1 gnd {bgain}
R5a outx1 gnd {rload}

* Negative side
X1b inm2 inp2 outx2 vdd vss opamp
R1b in2 inp2 {rload/2}
R2b inp2 gnd {rload/2}
R3b in2 inm2 {rload/2}
R4b inm2 out2 {rload}
E1b out2 gnd outx2 gnd {bgain}
R5b outx2 gnd {rload}

* offset voltage
X2 inm3 gnd osx vdd vss opamp
R12 gnd inm3 {rload/2}
R22 inm3 os {rload}
E2 os gnd osx gnd {bgain}
R32 osx gnd {rload}
